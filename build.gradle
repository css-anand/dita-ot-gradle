plugins {
    id 'com.gradle.plugin-publish' version '0.9.1'
}

apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'codenarc'

allprojects {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

defaultTasks 'check'

dependencies {
    compile gradleApi()

    compile 'commons-io:commons-io:1.4'

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }

    testCompile ('com.github.dita-ot:dost:2.1.2') {
        exclude group: 'commons-io', module: 'commons-io'
    }

    testCompile 'xml-resolver:xml-resolver:1.2'
    testCompile 'com.ibm.icu:icu4j:54.1'
}

repositories {
    jcenter()
}

codenarc {
    toolVersion = '0.24.1'
    configFile = file('config/codenarc/codenarc.groovy')
}

File ditaHome = new File("${projectDir}/dita-ot/src/main")

test {
    dependsOn 'integrate'
    systemProperty 'dita.home', System.getProperty('dita.home', ditaHome.getPath())
    systemProperty 'examples.dir', new File(projectDir, 'examples')
}

group = 'com.github.eerohele'
version = '0.3.2'

task integrate(dependsOn: ':dost:copyInstall') << {
    ant.ant([antfile: 'integrator.xml', dir: ditaHome])
}

task publish(dependsOn: ['check'])

pluginBundle {
    website = 'https://github.com/eerohele/dita-ot-gradle'
    vcsUrl = 'https://github.com/eerohele/dita-ot-gradle.git'
    description = 'A Gradle plugin for running DITA Open Toolkit'
    tags = ['dita', 'dita-ot', 'documentation']

    plugins {
        DitaOtPlugin {
            id = 'com.github.eerohele.dita-ot-gradle'
            displayName = 'DITA-OT Gradle Plugin'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
