plugins {
    id 'com.github.eerohele.dita-ot-gradle' version '0.4.2'
    id 'com.gradle.plugin-publish' version '0.9.8'
    id 'com.moowork.node' version '0.13'
    id 'com.moowork.gulp' version '0.13'
}

apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'codenarc'
apply plugin: 'java-gradle-plugin'

allprojects {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

defaultTasks 'check'

dependencies {
    compile gradleApi()
    compile 'commons-io:commons-io:1.4'

    testCompile gradleTestKit()

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.guava:guava:19.0'
    testCompile 'xml-resolver:xml-resolver:1.2'
}

repositories {
    jcenter()
}

codenarc {
    toolVersion = '1.0'
    configFile = file('config/codenarc/codenarc.groovy')
}

File ditaHome = new File(projectDir, 'dita-ot/src/main')
String docsDir = new File(projectDir, 'docs').getPath()

test {
    dependsOn 'setupDitaOt'
    systemProperty 'dita.home', System.getProperty('dita.home', ditaHome.getPath())
    systemProperty 'examples.dir', new File(projectDir, 'examples')
}

group = 'com.github.eerohele'
version = '0.5.0'

task buildDitaOt(type: GradleBuild) {
    buildFile = "$projectDir/dita-ot/build.gradle"
    tasks = ['buildLocal']
}

task symlink {
    ant.symlink(
        action: "delete",
        link: "$ditaHome/plugins/io.github.eerohele"
    )

    ant.symlink(
        resource: "${projectDir}/src/main/dita-ot/io.github.eerohele",
        link: "$ditaHome/plugins/io.github.eerohele"
    )
}

task setupDitaOt(dependsOn: [buildDitaOt, symlink])

ditaOt.dir ditaHome

task copyResources(type: Copy) {
    from "$projectDir/src/main/resources/data.json"
    into docsDir
}

dita {
    dependsOn setupDitaOt
    input "$projectDir/src/main/dita/index.ditamap"
    output docsDir

    filter "$projectDir/src/main/ditaval/default.ditaval"
    transtype 'html5'
    devMode true

    properties {
        property(name: 'processing-mode', value: 'strict')
        property(name: 'dita.input.valfile', location: "$projectDir/src/main/ditaval/default.ditaval")
        property(name: 'args.xhtml.toc', value: 'toc')
    }
}

task docs(dependsOn: [
    dita, copyResources, gulp_styles, gulp_scripts
])


task publish(dependsOn: [docs, check])

tasks.gulp_styles.dependsOn('installGulp', 'npmInstall')
tasks.gulp_scripts.dependsOn('installGulp', 'npmInstall')

npm_install {
    args = ['--loglevel', 'error']
}

pluginBundle {
    website = 'https://github.com/eerohele/dita-ot-gradle'
    vcsUrl = 'https://github.com/eerohele/dita-ot-gradle.git'
    description = 'A Gradle plugin for running DITA Open Toolkit'
    tags = ['dita', 'dita-ot', 'documentation']

    plugins {
        DitaOtPlugin {
            id = 'com.github.eerohele.dita-ot-gradle'
            displayName = 'DITA-OT Gradle Plugin'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}
